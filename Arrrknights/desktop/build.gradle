apply plugin: "java"

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = appJavaCompatibility

ext.appMainClass = "com.shatteredpixel.shatteredpixeldungeon.desktop.DesktopLauncher"
processResources {
    from new File(project(':core').projectDir, "/src/main/assets")
    from new File(project(':desktop').projectDir,"/src/main/assets")
}

sourceSets {
    debug
    release
}

task debug(dependsOn: classes, type: JavaExec) {
    classpath = sourceSets.debug.runtimeClasspath + sourceSets.main.runtimeClasspath
    ignoreExitValue = true

    main = appMainClass
    systemProperty 'Specification-Title', appName
    systemProperty 'Implementation-Title', appPackageName
    systemProperty 'Specification-Version', appVersionName + "-INDEV"
    systemProperty 'Implementation-Version', appVersionCode

    dependencies {
        debugImplementation project(':services:updates:debugUpdates')
        debugImplementation project(':services:news:debugNews')
    }
}

//need a separate task to compile dependencies first, seeing as we're setting them up in an odd way
task compileForRelease(dependsOn: classes, type: JavaCompile){
    classpath = sourceSets.release.runtimeClasspath + sourceSets.main.runtimeClasspath
}

task release(dependsOn: compileForRelease, type: Jar) {
    duplicatesStrategy 'exclude'
    from sourceSets.main.output
    from { sourceSets.release.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) } }
    from { configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) } }

    manifest {
        attributes 'Main-Class': appMainClass
        attributes 'Implementation-Title': appPackageName
        attributes 'Specification-Title': appName
        attributes 'Specification-Version': appVersionName
        attributes 'Implementation-Version': appVersionCode
    }

    dependencies {
        releaseImplementation project(':services:updates:githubUpdates')
        releaseImplementation project(':services:news:shatteredNews')
    }
}

dependencies {
    implementation project(':core')

    implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdxControllersVersion"

    //we use LWJGL tinyFD directly to display crash messages
    implementation "org.lwjgl:lwjgl-tinyfd:3.3.3"
    implementation "org.lwjgl:lwjgl-tinyfd:3.3.3:natives-windows"
    implementation "org.lwjgl:lwjgl-tinyfd:3.3.3:natives-macos"
    implementation "org.lwjgl:lwjgl-tinyfd:3.3.3:natives-macos-arm64"
    implementation "org.lwjgl:lwjgl-tinyfd:3.3.3:natives-linux"
    implementation "org.lwjgl:lwjgl-tinyfd:3.3.3:natives-linux-arm64"

    implementation project(':services:updates:githubUpdates')
    implementation project(':services:news:shatteredNews')
}